import org.gradle.internal.os.OperatingSystem

allprojects {
    repositories {
        mavenCentral()
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}

def build_directory = rootProject.buildDir
allprojects {
    def path = project.path.substring(1).replaceAll(":", "/")
    if (project == rootProject) path = project.name
    project.buildDir = new File(build_directory, path)
    project.ext.resourcesRootDir = new File(project.buildDir, "resources")
    project.ext.resourcesName = path
    project.ext.resourcesDir = new File(project.resourcesRootDir, project.resourcesName);

    project.ext.binaries_files = []

    task clean_resources(type: Delete) {
        delete project.resourcesDir
    }

    task copy_resources(type: Copy) {
        from 'resources'
        into project.resourcesDir
    }

    task build_resources() {
        dependsOn clean_resources, copy_resources
    }

    plugins.withType(CppPlugin).whenPluginAdded {
        build.dependsOn build_resources
    }
}

ext.apply_cpp = { project ->
    project.apply plugin: 'cpp'

    if (OperatingSystem.current().isWindows()) {
        project.apply plugin: 'visual-studio'
    }
}

apply from: 'native.gradle'