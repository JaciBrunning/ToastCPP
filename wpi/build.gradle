// We had to rewrite NTCore's build.gradle a little bit to fit into Toast's overall
// build system

ext.wpi_project_dir = project.projectDir

if (!isNoNTCore) {
    project(":wpi:nt") {
        model {
            components {
                ntcore(NativeLibrarySpec) {
                    component_spec(it, project)
                    targetPlatform "roborio-arm"
                    targetPlatform "any-32"
                    targetPlatform "any-64"
                    sources {
                        cpp {
                            source {
                                srcDirs = ['../ntcore/src']
                                includes = ['**/*.cpp']
                            }
                            exportedHeaders {
                                srcDirs = ['../ntcore/include', '../ntcore/src']
                                includes = ['**/*.h']
                            }
                        }
                    }

                    binaries.withType(NativeBinarySpec) {
                        if (toolChain in VisualCpp) {
                            cppCompiler.args '/EHsc', '/DNOMINMAX', '/D_SCL_SECURE_NO_WARNINGS', '/D_WINSOCK_DEPRECATED_NO_WARNINGS'
                            linker.args '/DEF:ntcore.def'
                        }
                    }
                }
            }
        }
    }
}

project(":wpi:wpilib") {
    task deferBuild(type: GradleBuild) {
        buildFile = '../allwpilib/build.gradle'
        tasks = [':wpilibc:build', 'wpilibcZip']

        doLast {
            delete file("build")
            copy {
                from zipTree(file("../allwpilib/wpilibc/build/wpilibc.zip"))
                into file("build")
            }
        }
    }
}